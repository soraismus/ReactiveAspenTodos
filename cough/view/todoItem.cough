{ Bridge, React } = require '../../vendor/reactive-aspen'
{ addons, DOM }   = React
{ classSet }      = addons
{ div, li }       = DOM

{ $button, $checkbox, $label, $text } = Bridge.adapters 

fields = [
  [$checkbox, 'completion-toggle']
  [$button,   'destroy-button'   ]
  [$text,     'todo-item-input'  ]
  [$label,    'todo-item-label'  ]
]

includeIndex = (label, index) ->
  { index, label }

indexifyAdapter = ([adapter, label]) -> (index) ->
  adapter (includeIndex (label, index))

[completionToggle, destroyButton, todoItemInput, todoItemLabel] =
  fields.map indexifyAdapter

TodoItem = (todoProps, index) ->
  { completed, editing, editText, title } = todoProps
  className = classSet { completed, editing }

  li
    key       : "todo-item-#{index}"
    className : className
    div
      className : 'view'
      completionToggle(index)
        className : 'toggle'
        checked   : completed
        onChange  : true
      todoItemLabel(index)
        onDoubleClick : true
        title
      destroyButton(index)
        className : 'destroy'
        onClick   : true
    todoItemInput(index)
      className : 'edit'
      value     : editText
      onBlur    : true
      onChange  : true
      onKeyDown : true

module.exports = TodoItem
