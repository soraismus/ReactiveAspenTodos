{ active, getTodos }      = require '../todo-utilities'
{ appStateProperty }      = require '../vendor/Aspen'
{ compose, store, uuid }  = require '../utilities'
{ NAMESPACE }             = require '../namespace'
{ transforms, utilities } = require '../vendor/Pando'

{ connect, getDispatcher, onValue, plugIntoTerminus } =
  require '../vendor/Controller'

{ doAsync }            = utilities
{ filtering, mapping } = transforms

TodoList = require './todoList'
AppState = require './appState'

addTodo = \title \appState ->
  return appState unless title
  AppState.resetTodos (appState, TodoList.addTodo (appState.todos, title))

cacheAppData = \appState ->
  store (NAMESPACE, appState)
  appState

editAppState = \capsule \appState ->
  # -----------------------------------------------------------------------
  todo = findTodo (appState.todos, capsule.id)
  storeOrigTitle todo.title
  # -----------------------------------------------------------------------
  AppState.resetEditing (appState, capsule.id)

endEditing = \getText \capsule ->
  # -----------------------------------------------------------------------
  text = getText ()
  setEventTgtValue (capsule, text)
  # -----------------------------------------------------------------------
  \appState ->
    newTodos = if text
        TodoList.recaptionTodo (appState.todos, capsule.id, text)
      else
        TodoList.removeTodo (appState.todos, capsule.id)
    AppState.reset (appState, { editing: null, todos: newTodos })

extractNewTodo = \capsule ->
  # -----------------------------------------------------------------------
  caption = capsule.event.target.value.trim ()
  setEventTgtValue (capsule, '')
  # -----------------------------------------------------------------------
  addTodo caption

filteringKey = \key ->
  filtering \capsule -> capsule.event.keyCode == key

findTodo = (todos, id) ->
  for todo, index in todos
    return todo if todo.id == id

removeCompletedTodos = \capsule \appState ->
  AppState.resetTodos (appState, TodoList.removeCompleted appState.todos)

removeTodo = \capsule \appState ->
  AppState.resetTodos ( 
    appState, TodoList.removeTodo (appState.todos, capsule.id))

[restoreOrigTitle, saveCurrentTitle, storeOrigTitle, storeTitle] = do ->
  editText     = null
  originalText = null

  restoreOrigTitle = ->
    editText = originalText
  saveCurrentTitle = ->
    if editText then editText.trim () else editText
  storeOrigTitle = \title ->
    editText = originalText = title
  storeTitle = \title ->
    editText = title

  [restoreOrigTitle, saveCurrentTitle, storeOrigTitle, storeTitle]

setEventTgtValue = (capsule, text) ->
  capsule.event.target.value = text

toggleAllTodos = \capsule \appState ->
  AppState.resetTodos (appState, TodoList.toggleAll appState.todos)

toggleTodo = \capsule \appState ->
  AppState.resetTodos (
    appState, TodoList.toggleTodo (appState.todos, capsule.id))

filteringEnter  = filteringKey 13
filteringEscape = filteringKey 27

nodes = [
  '$toggle-clicks'
  '$toggle-all-clicks'
  '$clear-clicks'
  '$new-todo-keydowns'
  '$destroy-clicks'
  '$edit-blurs'
  '$edit-keydowns'
  '$edit-keydowns'
]

transforms = [
  mapping toggleTodo
  mapping toggleAllTodos
  mapping removeCompletedTodos
  compose [filteringEnter, (mapping extractNewTodo)]
  mapping removeTodo
  mapping (endEditing saveCurrentTitle)
  compose [filteringEnter,  mapping (endEditing saveCurrentTitle)]
  compose [filteringEscape, mapping (endEditing restoreOrigTitle)]
]

connect nodes 'cacher' -> transforms

plugIntoTerminus 'cacher', ->
  mapping \transform -> ; cacheAppData transform ;

plugIntoTerminus '$todo-label-doubleclicks', ->
  mapping editAppState

onValue 'todo-in-edit', \capsule ->
  storeTitle (capsule.event.target.value)
