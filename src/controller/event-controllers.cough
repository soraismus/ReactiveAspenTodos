{ active, getTodos }      = require '../todo-utilities'
{ appStateProperty }      = require '../vendor/Aspen'
{ compose, store, uuid }  = require '../utilities'
{ NAMESPACE }             = require '../namespace'
{ transforms, utilities } = require '../vendor/Pando'

{ connect, getDispatcher, onValue, plugIntoTerminus } =
  require '../vendor/Controller'

{ doAsync }            = utilities
{ filtering, mapping } = transforms

activateAll = \todos ->
  todos.forEach \todo -> todo.completed = false

addTodo = \title \appState ->
  return appState unless title
  appState.todos.push (createTodo title)
  appState

cacheAppData = \appState ->
  store (NAMESPACE, appState)
  appState

completeAll = \todos ->
  todos.forEach \todo -> todo.completed = true

createTodo = \title ->
  { completed: false, id: uuid (), title: title }

editAppState = \capsule \appState ->
  { id }   = capsule
  { todo } = findTodo (appState.todos, id)
  storeOrigTitle todo.title
  appState.editing = id
  appState

findTodo = (todos, id) ->
  for todo, index in todos
    return { todo, index } if todo.id == id

endEditing = \getText \capsule ->
  text = getText ()
  capsule.event.target.value = text

  \appState ->
    { todo, index } = findTodo (appState.todos, capsule.id)
    appState.editing = null
    todo.title       = text
    if text then appState else removeTodo (index, appState)

extractNewTodo = \capsule ->
  value = capsule.event.target.value.trim ()
  capsule.event.target.value = ''
  addTodo value

filteringKey = \key ->
  filtering \capsule -> capsule.event.keyCode == key

[restoreOrigTitle, saveCurrentTitle, storeOrigTitle, storeTitle] = do ->
  editText     = null
  originalText = null

  restoreOrigTitle = ->
    editText = originalText
  saveCurrentTitle = ->
    if editText then editText.trim () else editText
  storeOrigTitle = \title ->
    editText = originalText = title
  storeTitle = \title ->
    editText = title

  [restoreOrigTitle, saveCurrentTitle, storeOrigTitle, storeTitle]

removeCompleted = \capsule \appState ->
  appState.todos = appState.todos.filter active
  appState

removeTodoByID = \capsule \appState ->
  { index } = findTodo (appState.todos, capsule.id)
  removeTodo (index, appState)

removeTodo = (index, appState) ->
  { todos } = appState
  { completed } = todos[index]
  todos.splice (index, 1)
  appState

toggleAllTodos = \capsule \appState ->
  { todos } = appState
  allCompleted = todos.every \todo -> todo.completed
  manage = if allCompleted then activateAll else completeAll
  manage todos
  appState

toggleTodo = \capsule \appState ->
  { todo } = findTodo (appState.todos, capsule.id)
  todo.completed = !todo.completed
  appState

filteringEnter  = filteringKey 13
filteringEscape = filteringKey 27

nodes = [
  '$toggle-clicks'
  '$toggle-all-clicks'
  '$clear-clicks'
  '$new-todo-keydowns'
  '$destroy-clicks'
  '$edit-blurs'
  '$edit-keydowns'
  '$edit-keydowns'
]

transforms = [
  mapping toggleTodo
  mapping toggleAllTodos
  mapping removeCompleted
  compose [filteringEnter, (mapping extractNewTodo)]
  mapping removeTodoByID
  mapping (endEditing saveCurrentTitle)
  compose [filteringEnter,  mapping (endEditing saveCurrentTitle)]
  compose [filteringEscape, mapping (endEditing restoreOrigTitle)]
]

connect nodes 'cacher' -> transforms

plugIntoTerminus 'cacher', ->
  mapping \transform -> ; cacheAppData transform ;

plugIntoTerminus '$todo-label-doubleclicks', ->
  mapping editAppState

onValue 'todo-in-edit', \capsule ->
  storeTitle (capsule.event.target.value)
